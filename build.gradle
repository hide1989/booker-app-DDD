// Common for all projects
allprojects {
  apply plugin: 'java'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 1.11
  targetCompatibility = 1.11

  repositories {
    mavenCentral()
  }

  dependencies {
    // Prod
    implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
    implementation 'com.vlkan.log4j2:log4j2-logstash-layout:0.19'

    // Test
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testCompile "org.mockito:mockito-core:2.+"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
  }

  test {
    useJUnitPlatform()

    testLogging {
      events "passed", "skipped", "failed"
    }
  }


  task view_paths {
    doLast { task ->
      println "$task.project.name"
      println "------------------"
      println "Main: $sourceSets.main.java.srcDirTrees"
      println "   Resources: $sourceSets.main.resources.srcDirTrees"
      println "Tests: $sourceSets.test.java.srcDirTrees"
      println "   Resources: $sourceSets.test.resources.srcDirTrees"
    }
  }
}

// All subprojects (located in src/*)
subprojects {
  group = "booker.app.${rootProject.name}"

  sourceSets {
    main {
      java { srcDirs = ['main'] }
      resources { srcDirs = ['main/resources'] }
    }

    test {
      java { srcDirs = ['test'] }
      resources { srcDirs = ['test/resources'] }
    }
  }

  dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation 'org.hibernate:hibernate-core:5.4.5.Final'
    implementation 'org.springframework:spring-orm:5.1.10.RELEASE'
    implementation 'org.apache.tomcat:tomcat-dbcp:9.0.26'
    implementation 'com.sun.xml.bind:jaxb-impl:2.1.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    runtime 'mysql:mysql-connector-java:8.0.17'

    testImplementation rootProject.sourceSets.main.output // indica que todos los subproyectos implementaran la suit de test del padre o proyecto raiz
    //testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.8.RELEASE'
    testImplementation("org.springframework.boot:spring-boot-starter-test")

    //inyecta la share como dependencia de los demas subproyectos
    if(project.name != "shared"){
      implementation project(":shared")
      testImplementation project(":shared").sourceSets.test.output
    }
  }

  bootJar {
    enabled = false
  }

  jar {
    enabled = true
  }
}

// Main project (located in app/)
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE")
  }
}

sourceSets {
  main {
    java { srcDirs = ['app/main'] }
    resources { srcDirs = ['app/main/resources'] }
  }

  test {
    java { srcDirs = ['app/test'] }
    resources { srcDirs = ['app/test/resources'] }
  }
}

apply plugin: 'application'

bootJar {
  baseName = 'booker-java-DDD'
  version = '0.0.1'
}

mainClassName = 'booker.app.app.Starter'

dependencies {
  compile("org.springframework.boot:spring-boot-starter-web")

  implementation project(":shared")
  implementation project(":maroos")

  testImplementation("org.springframework.boot:spring-boot-starter-test")
}

